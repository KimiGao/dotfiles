# -*- mode: sh; -*-

##### Environment variables
. ~/.config/zsh/env

##### History
export HISTFILE=~/.histfile
export HISTSIZE=1000
export SAVEHIST=1000
export DIRSTACKSIZE=16
setopt hist_ignore_space
setopt appendhistory

##### Colors
autoload -U colors
colors
# Nice colors in ls. Changing TERM is needed because dircolors doesn't know
# about rxvt-256color...
eval `TERM=xterm-256color dircolors -b`

# Add some more colors!
function _add_lscolors() {
    local color ext mimetype
    mimetype=shift; color=shift
    for ext in $(grep "$mimetype" /etc/mime.types | cut -f 2-); do
        LS_COLORS="$LS_COLORS:*.$ext=$color"
    done
    export LS_COLORS
}
_add_lscolors "audio/" "00;36"
_add_lscolors "image/" "01;35"
_add_lscolors "video/" "01;35"

##### Prompt
autoload -U promptinit
promptinit
setopt promptsubst
if [[ $TERM == "dumb" ]]; then    # in emacs
    export PS1='%(?..[%?])%!:%~%# '
    # for tramp to not hang, need the following. cf:
    # http://www.emacswiki.org/emacs/TrampMode
    unsetopt zle
    unsetopt prompt_cr
    unsetopt prompt_subst
    unfunction precmd  &>/dev/null
    unfunction preexec &>/dev/null
else
    # VCS info in the right prompt, based on http://kriener.org/articles/2009/06/04/zsh-prompt-magic
    autoload -Uz vcs_info

    FMT_BRANCH="%F{magenta}%s:%F{blue}%b%B%F{magenta}%u%c%%b%F{default}"
    FMT_ACTION="(%F{blue}%a%F{default})"

    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' unstagedstr '*'
    zstyle ':vcs_info:*' stagedstr   '^'
    zstyle ':vcs_info:*' actionformats "${FMT_BRANCH}${FMT_ACTION} / "
    zstyle ':vcs_info:*' formats       "${FMT_BRANCH} / "
    zstyle ':vcs_info:*' nvcsformats   ""

    PS1="%B" # Bold font
    PS1+="%(!..%(1000#..%F{green}%n))" # Username in green if it's not UID 1000 or root

    # Display hostname in PS1 if we're running on SSH
    if [[ ! -z "$(who am i)" ]]; then
        PS1+="%(!..%(1000#..%F{yellow}@))%F{white}%m "
    fi

    PS1+='%B%F{white}${ps1_path} ' # Path
    PS1+="%(!.%F{red}.%F{yellow})%# " # %/# sign in yellow or red (root)
    PS1+="%F{default}%b" # Restore normal font
    export PS1

    RPS1="%(?..%B%F{red}%?%b%F{default} / )" # Status code if red if not 0
    RPS1+='${vcs_info_msg_0_}' # VCS info
    RPS1+="%B%F{cyan}%*" # Time
    RPS1+="%F{default}%b" # Restore normal font
    export RPS1

    function precmd {
        # Terminal title
        case $TERM in
            xterm*|rxvt*|Eterm|screen) print -Pn "\e]0;%n@%m: %~\a" ;;
        esac

        # Update VCS prompt (without telling stuff like "fatal: This operation
        # must be run in a work tree" if we are in a .git dir)
        vcs_info 2>/dev/null

        # Path displayed in PS1 - collapse $HOME but not other variables
        ps1_path="${PWD/#$HOME/~}"
        # Collapse each dir(but the last) to its first letter
        # From  http://git.io/ffi9_A, no idea how it works :)
        if [[ ! "${ps1_path}" == "~" ]]; then
            ps1_path="${${${(@j:/:M)${(@s:/:)ps1_path}##.#?}:h}%/}/${ps1_path:t}"
        fi
        export ps1_path
    }
fi

##### Completion
# Load custom scripts (but not as root, or compinit will complain about "insecure directories and files")
if [[ $UID -ne 0 ]]; then
    fpath=($HOME/.config/zsh/completion $fpath)
    autoload -U $HOME/.config/zsh/completion/_*(.:t)
    for f in $HOME/.config/zsh/completion/*.zsh(.); do
        source $f
    done
fi

# Now init the completion system
autoload -Uz compinit
compinit
zstyle :compinstall filename "$HOME/.zshrc"
zstyle ':completion::complete:*' use-cache 1

unsetopt list_ambiguous
setopt auto_remove_slash
setopt glob_dots
setopt chase_links
setopt hist_verify
setopt hist_ignore_all_dups
setopt auto_cd
setopt auto_pushd
setopt correctall

# Avoid repeating file names when completing these commands
zstyle ':completion:*:rm:*' ignore-line yes
zstyle ':completion:*:mv:*' ignore-line yes
zstyle ':completion:*:cp:*' ignore-line yes

# Some colors
zstyle ':completion:*:*:kill:*:processes' list-colors "=(#b) #([0-9]#)*=36=31"

# Useful stuff for writing and debugging completion functions
#zstyle ':completion:*' verbose yes
#zstyle ':completion:*:descriptions' format '%B%d%b'
#zstyle ':completion:*:messages' format '%d'
#zstyle ':completion:*:warnings' format 'No matches for: %d'
#zstyle ':completion:*' group-name ''

##### Various options
setopt extendedglob

# Don't send "HUP" to running jobs when logging out
unsetopt hup

# zmv is awesome
autoload -Uz zmv

# Reset WORDCHARS so it's easier to navigate between words.
# Default value: export WORDCHARS='*?_-.[]~=/&;!#$%^(){}<>'
export WORDCHARS=''

##### Tell other scripts this one has been loaded
export PROFILE_LOADED=1
